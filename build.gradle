buildscript {
    ext.kotlin_version = '1.2.61'
    dependencies {
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:5.1.0"
        classpath "com.netflix.nebula:gradle-lint-plugin:9.3.2"
        classpath "gradle.plugin.org.jmailen.gradle:kotlinter-gradle:1.15.1"
        classpath "org.ajoberstar:gradle-git:1.7.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
        classpath "org.eclipse.jgit:org.eclipse.jgit:4.4.1.201607150455-r"
        classpath "net.researchgate:gradle-release:2.6.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://dl.bintray.com/jetbrains/spek/" }
    }
}

apply plugin: 'base'
apply plugin: 'nebula.lint'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'idea'

apply from: "${project.rootDir}/release.gradle"
apply from: "${project.rootDir}/kotlin.gradle"


repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

group 'com.youcruit'
description = """
                Differs from the original implementation in that it supports wildcards, supports rejecting SUBSCRIBEs and
                SENDs. The latter two are supported through hitting endpoints which may deny or pass the message through 
            """.stripMargin()


idea {
    module {
        name = "Atmosphere Stomp NG"
        outputDir = file('build/idea-out')
        testOutputDir = file('build/idea-testout')
        downloadSources = true
    }
}

dependencyUpdates {
    revision = 'release'
}

gradleLint {
    criticalRules = ['all-dependency', 'duplicate-dependency-class']
    rules = ['archaic-wrapper']
    excludedRules = ['undeclared-dependency']
    autoLintAfterFailure = false
    alwaysRun = false
}


task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output.classesDirs
}

artifacts {
    archives testJar
}

dependencies {
    api "org.atmosphere:atmosphere-runtime:${atmosphere_version}"

    implementation "javax.servlet:javax.servlet-api:3.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    api "org.slf4j:slf4j-api:$slf4j_version"

    testImplementation "org.slf4j:slf4j-simple:$slf4j_version"
    testImplementation "junit:junit:4.12"
    testImplementation "javax.inject:javax.inject:1"

    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC3"

}

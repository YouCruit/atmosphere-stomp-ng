apply plugin: 'net.researchgate.release'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        name = project.name
        repo = 'YouCruit'
        userOrg = 'youcruit'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/YouCruit/atmosphere-stomp-ng.git'
        publicDownloadNumbers = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'atmosphere-stomp-ng'
            from components.java
            artifact tasks.sourceJar
            artifact tasks.javadocJar
            artifact tasks.testJar
        }
    }
}

release {
    revertOnFail = false

    // $version needs to be quoted so it is resolved at tag time
    tagTemplate = "atmosphere-ng-\$version-${new Date().format('yyyy-MM-dd-HHmm')}"


    git {
        requireBranch = ''
    }

}

tasks.release.group = 'Release'
afterReleaseBuild.dependsOn bintrayUpload